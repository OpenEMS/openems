plugins {
    id "com.github.node-gradle.node" version "1.3.0"
    id "org.ajoberstar.reckon" version "0.9.0"
}

reckon {
    scopeFromProp()
    stageFromProp('dev', 'final')
}

/*
 * Updates the OpenemsConstants file with the generated Version number
 */
task updateOpenemsConstants {
    def version = project.version.version.getVersion()
    def versionString = version.getPreReleaseVersion()
    if (!version.getBuildMetadata().isEmpty()) {
        versionString += "+" + version.getBuildMetadata()
    }
    def openemsConstantsFile = new File('io.openems.common/src/io/openems/common/OpenemsConstants.java')
    def constants = openemsConstantsFile.getText('UTF-8')
    constants = constants.replaceAll('(short VERSION_MAJOR = ).*;', '$1' + version.getMajorVersion() + ';')
    constants = constants.replaceAll('(short VERSION_MINOR = ).*;', '$1' + version.getMinorVersion() + ';')
    constants = constants.replaceAll('(short VERSION_PATCH = ).*;', '$1' + version.getPatchVersion() + ';')
    constants = constants.replaceAll('(String VERSION_STRING = \").+(\";)', '$1' + versionString + '$2')
    openemsConstantsFile.write(constants, 'UTF-8')
}

/*
 * Build OpenEMS Edge and Backend Components
 */
task buildComponents(dependsOn: updateOpenemsConstants) {
    subprojects.each { proj ->
        if (proj.tasks.findAll { it.name == 'jar' }) {
            dependsOn(proj.jar)
        }
    }
}

/*
 * Build OpenEMS Edge fat-jar
 */
task buildEdge(dependsOn: buildComponents) {
    dependsOn ":io.openems.edge.application:export.EdgeApp"
    doLast {
        copy {
            from file("${project(":io.openems.edge.application").distsDir}/executable/EdgeApp.jar")
            into file("${buildDir}")
            rename ("EdgeApp.jar", "openems-edge.jar")
        }
    }
}

/*
 * Build OpenEMS Backend fat-jar
 */
task buildBackend(dependsOn: buildComponents) {
    dependsOn ":io.openems.backend.application:export.BackendApp"
    doLast {
        copy {
            from file("${project(":io.openems.backend.application").distsDir}/executable/BackendApp.jar")
            into file("${buildDir}")
            rename ("BackendApp.jar", "openems-backend.jar")
        }
    }
}

/*
 * Javadoc
 */
task aggregatedJavadocs(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
        destinationDir = file("$buildDir/www/javadoc")
    title = "OpenEMS Javadoc"
    subprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}

/*
 * Antora docs
 */
node {
    nodeModulesDir = file("${projectDir}/doc/build")
}

task antora(type: NodeTask) {
    dependsOn npmInstall
    script = file("doc/build/node_modules/@antora/cli/bin/antora")
    args = ["doc/build/site.yml"]
    doLast {
        copy {
            from "doc/build/.nojekyll", "doc/build/CNAME"
            into "${buildDir}/www"
        }
    }
}